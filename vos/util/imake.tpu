!
! TPU script to strip blank lines and lines starting with # from a file.
! Also converts "@@" into a line break.
!
! Used with the "vimake" program under VMS.
!
input_file := GET_INFO(COMMAND_LINE, 'file_name');
main_buffer := CREATE_BUFFER('main', input_file);
POSITION(BEGINNING_OF (main_buffer));
!
our_pat := ( LINE_BEGIN + LINE_END ) | ( LINE_BEGIN + "#" ) |
	( LINE_BEGIN + " " + LINE_END ) | ( LINE_BEGIN + "" + LINE_END );
LOOP
  found := SEARCH_QUIETLY(our_pat, FORWARD, EXACT);
  EXITIF found = 0;
  POSITION(found);
  ERASE_LINE;
ENDLOOP;
!
POSITION(BEGINNING_OF (main_buffer));
eol_pat := "@@";
LOOP
  found := SEARCH_QUIETLY(eol_pat, FORWARD, EXACT);
  EXITIF found = 0;
  ERASE(found);
  POSITION(found);
  SPLIT_LINE;
ENDLOOP;
!
WRITE_FILE(main_buffer, GET_INFO(COMMAND_LINE, 'output_file'));
QUIT;
