procedure help=* options=nointerrupt
parm out	string
parm primary	type=integer
parm miplbox	type=(string,9)
parm job	type=(string,15) default=""
parm comment	type=(string,127) count=0:1 default=--
parm device string count=0:1 default=--
body
jsave-image @out planes=IMAGE 'STRETCH 'COLORGRAPH device=@device
let _onfail="goto nomrps"
barne @out primary=@primary miplbox=@miplbox job=@job comment=@comment 'notify
goto end
nomrps>
if ($skey = "TAE-NOPROCESS")
  putmsg "Sorry, can't run BARNE from inside a program." "VIDS-NOPHOTO"
  write "The temporary image file has been created, so please run BARNE"
  write "on that image after you exit the currently running program."
else
  putmsg "Sorry, JPHOTO needs the MIPL MRPS software to run." "VIDS-NOMRPS"
end-if
end>
end-proc
.title
VIDS command JPHOTO
.help
JPHOTO will print an image of what's on the screen using MIPL's film
recorders via the MRPS (Multiple Recorder Production System) software.
Since MRPS is not delivered outside of MIPL, external sites will not
be able to use JPHOTO.

JPHOTO does only a very simple, quick print of what's on the screen.
It needs a filename, which is used as a temporary file only.  When
the print is complete, the file will be deleted.

JPHOTO operates by first calling JSAVE-IMAGE to save the image to the
temporary file, with the 'STRETCH and 'COLORGRAPH options turned on.
These options cause the image to be sent through the hardware lookup
table, and cause the graphics overlay to be "burned in" to the image,
becoming part of it.  See the help for JSAVE-IMAGE for more details.
.page
JPHOTO then calls BARNE, which is the user-interface program for
MRPS.  Several parameters are required for BARNE, notably your phone
number and your MIPL box number.  The other parameters are optional.
See the help for BARNE for more details.

JPHOTO is very limited in its operation, as it only has a few options.
If you want more control over your photo, use the JSAVE-IMAGE and BARNE
programs separately.

JPHOTO cannot be run from inside another program via passthru or XVCOMMAND,
because BARNE is an independent program.  If you try to run it from another
program, VIDS will print a warning message.  The file will be created, and
you can get your print by simply manually running BARNE on that file after
you exit the currently running program.
.level1
.vari out
The temporary file to
use.  It will be deleted
when the photo is done.
.vari primary
Primary JPL extension where the
user can be contacted.
.vari miplbox
MIPL box number where the
finished products should be
delivered.
.vari job
Job name to help identify the
image.
.vari comment
Arbitrary user comments.
.vari device
Name of device to use
.level2
.variable out
The OUT parameter specifies a filename to use as a temporary file
during the recording process.  The file will be deleted when the
photo is finished.  The filename must be entered.

Please do not use the scratch disk SCX1:, or a mountable disk, as
the file might get automatically deleted or be otherwise unavailable
before MRPS can process it.  For the same reason, don't use temporary
VICAR files (without the .Zxx extension), as they are deleted when you
log off.  It is possible to use scratch disk SCX2:, since the file
deletion time is several days, but it is not recommended.
.vari primary
The PRIMARY parameter specifies the primary JPL telephone extension
where the user can be contacted if there are problems with processing
the image.  It is required by MRPS.
.vari miplbox
The MIPLBOX parameter specifies the MIPL user box number where the
finished products should be delivered.  It is required by MRPS.
.vari job
The JOB parameter specifies a job name to help identify the image.
It is an optional parameter.
.vari comment
The COMMENT parameter specifies the user's thoughts at the moment
and is recorded for image identification and posterity.  It is
an optional parameter.
.vari device
The DEVICE parameter specifies which display device to perform this
operation on.  This device will then become the current device for
future operations.

If DEVICE is not given, the current display device is used, i.e. the
last device you specified on any VIDS command.

A device must be JGRAB'ed before use.  See the help for JGRAB for more
details.
.end
